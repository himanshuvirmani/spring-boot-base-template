buildscript {
  repositories {
    mavenCentral()
    jcenter()
  }
  dependencies {
    classpath("org.springframework.boot:spring-boot-gradle-plugin:${spring_boot_version}")
    classpath("io.spring.gradle:dependency-management-plugin:0.5.2.RELEASE")
  }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'spring-boot'
apply plugin: 'io.spring.dependency-management'
defaultTasks 'bootRun'

jar {
  baseName = 'springbootdemo'
  version = '0.0.1-SNAPSHOT'
}

bootRepackage {
  mainClass = 'com.springboot.demo.Application'
}

springBoot {
  mainClass = 'com.springboot.demo.Application'
}

bootRun {
  addResources = false
}

if (project.hasProperty('prod')) {  //./gradlew -Pprod bootRun
  apply from: 'profile_prod.gradle'
} else if (project.hasProperty('fast')) {
  apply from: 'profile_fast.gradle'
} else {
  apply from: 'profile_dev.gradle'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
  mavenCentral()
  maven { url 'http://repo.spring.io/milestone' }
  maven { url 'http://repo.spring.io/snapshot' }
  maven { url 'https://repository.jboss.org/nexus/content/repositories/releases' }
  maven { url 'https://oss.sonatype.org/content/repositories/releases' }
  maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
  maven { url 'http://repo.maven.apache.org/maven2' }
}


dependencies {

  compile group: 'io.dropwizard.metrics', name: 'metrics-core'
  compile group: 'io.dropwizard.metrics', name: 'metrics-annotation', version: dropwizard_metrics_version

  compile group: 'org.springframework.boot', name: 'spring-boot-actuator'
  compile group: 'org.springframework.boot', name: 'spring-boot-autoconfigure'
  compile group: 'org.springframework.boot', name: 'spring-boot-starter-aop'
  compile group: 'org.springframework.boot', name: 'spring-boot-loader-tools'
  compile group: 'org.springframework.boot', name: 'spring-boot-starter-logging'
  compile group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa'
  compile group: 'org.springframework.boot', name: 'spring-boot-starter'
  compile group: 'org.springframework.boot', name: 'spring-boot-starter-jetty'
  compile(group: 'org.springframework.boot', name: 'spring-boot-starter-web') {
    exclude module: 'spring-boot-starter-tomcat'
  }
  compile 'org.springframework.statemachine:spring-statemachine-core:1.0.0.RC1'

  compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-json-org',
          version: jackson_version
  compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-hppc',
          version: jackson_version
  compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-joda',
          version: jackson_version
  compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-hibernate4',
          version: jackson_version
  compile group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: jackson_version

  compile(group: 'com.zaxxer', name: 'HikariCP', version: HikariCP_version) {
    exclude(module: 'tools')
  }
  compile group: 'commons-lang', name: 'commons-lang', version: commons_lang_version
  compile group: 'commons-io', name: 'commons-io', version: commons_io_version
  compile group: 'javax.inject', name: 'javax.inject', version: javax_inject_version
  compile group: 'javax.transaction', name: 'javax.transaction-api'
  compile group: 'joda-time', name: 'joda-time', version: joda_time_version
  compile group: 'joda-time', name: 'joda-time-hibernate', version: joda_time_hibernate_version
  compile group: 'org.hibernate', name: 'hibernate-core', version: hibernate_entitymanager_version
  compile group: 'org.hibernate', name: 'hibernate-envers'
  compile(group: 'org.hibernate', name: 'hibernate-ehcache') {
    exclude(module: 'ehcache-core')
  }
  compile group: 'org.hibernate', name: 'hibernate-validator'
  compile group: 'org.jadira.usertype', name: 'usertype.core', version: usertype_core_version

  compile group: 'org.projectlombok', name: 'lombok', version: lombok_version

  compile(group: 'io.springfox', name: 'springfox-swagger2', version: springfox_version) {
    exclude module: 'mapstruct'
  }
  compile group: 'io.springfox', name: 'springfox-swagger-ui', version: springfox_version

  runtime("com.h2database:h2")
  runtime("mysql:mysql-connector-java")

  testCompile group: 'com.jayway.awaitility', name: 'awaitility', version: awaility_version
  testCompile group: 'com.jayway.jsonpath', name: 'json-path'
  testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test'
  testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-jetty'
  testCompile group: 'org.assertj', name: 'assertj-core', version: assertj_core_version
  testCompile group: 'junit', name: 'junit'
  testCompile group: 'org.mockito', name: 'mockito-core'
  testCompile group: 'org.hamcrest', name: 'hamcrest-library'
}

configurations.all {
  resolutionStrategy {
    // force certain versions of dependencies (including transitive)
    //  *append new forced modules:
    force group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: jackson_version
    force group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: jackson_version
    force group: 'joda-time', name: 'joda-time', version: joda_time_version
  }
}

eclipse {
  classpath {
    containers.remove('org.eclipse.jdt.launching.JRE_CONTAINER')
    containers 'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.8'
  }
}

clean {
  delete "target"
}

task wrapper(type: Wrapper) {
  gradleVersion = '2.6'
}
